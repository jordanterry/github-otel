plugins {
    alias(libs.plugins.kotlin.jvm)
    alias(libs.plugins.kotlin.ksp)
    alias(libs.plugins.gmazzo.build.config)
    alias(libs.plugins.kotlin.serialisation)
    alias(libs.plugins.sort.dependencies)
}

group = 'uk.co.jordanterry'
version = '1.0-SNAPSHOT'

buildConfig {
    buildConfigField(String, 'GITHUB_API_KEY', providers.environmentVariable('GITHUB_API_KEY'))
}

dependencies {
    api platform(libs.okhttp.bom)
    api libs.kotlin.serialisation.core
    api libs.okhttp
    api libs.retrofit
    api projects.apis.github.api

    implementation libs.kotlinx.coroutines
    implementation libs.kotlinx.datetime

    testImplementation libs.junit5.jupiter.api
    testImplementation libs.kotest.assertions.core
    testImplementation libs.kotlinx.coroutines.test
    testImplementation libs.okhttp.mockwebserver.junit5

    testRuntimeOnly libs.junit5.jupiter.runtime

    ksp libs.moshi.kotlin.codegen
}

test {
    useJUnitPlatform()
}

kotlin {
    jvmToolchain(21)
    explicitApi()
}